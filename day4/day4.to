MAKE STRING input = READ("day4/input.txt");
MAKE ARRAY STRING lines = SPLIT(input, "\n");

MAKE INT game_len = (INT)lines;

MAKE INT sum = 0;
MAKE INT sum2 = 0;

MAKE ARRAY INT copies = [];
MAKE INT c = 0; DO {
    SET copies += [1];
    SET c += 1;
} WHILE (c < game_len);


MAKE INT i = 0; DO {
    MAKE STRING winstring = SPLIT(SPLIT(lines#i, ":")#1, "|")#0;
    MAKE STRING gamestring = SPLIT(SPLIT(lines#i, ":")#1, "|")#1;

    MAKE ARRAY STRING win = SPLIT(winstring, " ");

    MAKE INT result = 0;
    MAKE INT amount = 0;

    MAKE INT j = 0; DO {
        DO {
            SET result = (result << 1) + !result;
            SET amount += 1;
        } WHEN (CONTAINS(" " + gamestring + " ", " " + win#j + " "));
        SET j += 1;
    } WHILE (j < win);

    MAKE INT c = i+1; DO {
        SET copies#c += copies#i;
        SET c += 1;
    } WHILE (c < i + amount + 1 && c < game_len);

    SET sum += result;

    SET i += 1;
} WHILE (i < game_len);

DO SAYLN ("Part 1: " + sum);

SET c = 0; DO {
    SET sum2 += copies#c;
    SET c += 1;
} WHILE (c < game_len);

DO SAYLN ("Part 2: " + sum2);