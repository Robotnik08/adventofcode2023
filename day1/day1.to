MAKE STRING input = READ("day1/input.txt");
MAKE ARRAY STRING lines = SPLIT(input, "\n");

MAKE FUNC INT count (ARRAY STRING lines) {
    MAKE INT sum = 0;
    MAKE INT i = 0; DO { // for each line
        MAKE INT first = 0;
        MAKE INT last = 0;

        MAKE INT j = 0; DO { // for each char
            DO {
                DO {
                    SET first = lines#i#j - '0';
                } WHEN (!first);
                SET last = lines#i#j - '0';
            } WHEN (lines#i#j > '0' && lines#i#j <= '9');
            SET j += 1;
        } WHILE (j < lines#i);
        
        SET sum += first*10 + last;
        SET i += 1;
    } WHILE (i < lines);
    DO RETURN(sum);
};

MAKE FUNC ARRAY STRING replace_nums (ARRAY STRING lines) {
    MAKE ARRAY STRING full_num = [
        "zero", "one", "two", "three", "four", "five", "six", "seven", "eight", "nine"
    ];
    MAKE INT i = 0; DO {
        MAKE INT num = 1; DO {
            SET lines#i = REPLACE(lines#i, full_num#num, (STRING)(full_num#num#0) + num + full_num#num#-2);
            SET num += 1;
        } WHILE (num < 10);
        SET i += 1;
    } WHILE (i < lines);
    DO RETURN(lines);
};

DO SAYLN("Part 1: " + count(lines));
DO SAYLN("Part 2: " + count(replace_nums(lines)));